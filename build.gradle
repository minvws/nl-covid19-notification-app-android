// Top-level build file where you can add configuration options common to all sub-projects/modules.

buildscript {
    //androidx/google dependency versions
    ext.androidx_activity_version = "1.2.2"
    ext.androidx_appcompat_version = "1.3.0-rc01"
    ext.androidx_core_version = "1.3.2"
    ext.arch_core_version = "2.1.0"
    ext.androidx_espresso_version = "3.3.0"
    ext.androidx_fragment_version = "1.3.2"
    ext.androidx_lifecycle_version = "2.3.1"
    ext.androidx_nav_version = "2.3.5"
    ext.androidx_test_core_version = "1.3.0"
    ext.androidx_test_ext_junit_version = "1.1.2"
    ext.androidx_work_version = "2.5.0"
    ext.coroutines_version = "1.4.3"
    ext.kotlin_version = '1.4.32'
    ext.play_services_base_version = "17.6.0"
    ext.play_services_tasks_version = "17.2.1"
    //3rd-party dependency versions
    ext.groupie_version = "2.8.1"
    ext.jdk15to18_version = "1.66"
    ext.junit_version = "4.13.2"
    ext.moshi_version = "1.11.0"
    ext.mockito_version = "3.9.0"
    ext.mockitokotlin_version = "2.2.11"
    ext.okhttp3_version = "4.9.0"
    ext.retrofit_version = "2.9.0"
    ext.robolectric_version = "4.5.1"
    ext.timber_version = "4.7.1"
    ext.insetter_version = "0.5.0"

    repositories {
        google {
            content {
                includeGroup "com.google"
                includeGroup "com.android"
                includeGroupByRegex "com.google\\..*"
                includeGroupByRegex "com.android\\..*"
                includeGroupByRegex "androidx\\..*"
            }
        }
        mavenCentral()
        maven {
            url "https://plugins.gradle.org/m2/"
            content {
                includeGroup "com.github.triplet.gradle"
            }
        }
        jcenter() {
            content {
                includeGroup "org.jetbrains.trove4j"
            }
        }
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:4.2.0'
        classpath "com.osacky.flank.gradle:fladle:0.11.0"
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "com.diffplug.spotless:spotless-plugin-gradle:5.6.1"
        classpath "androidx.navigation:navigation-safe-args-gradle-plugin:$androidx_nav_version"
        classpath "com.github.triplet.gradle:play-publisher:2.8.0"
        classpath 'com.google.firebase:firebase-appdistribution-gradle:2.1.0'
        classpath "ru.cian:huawei-publish-gradle-plugin:1.1.0"
        // NOTE: Do not place your application dependencies here; they belong
        // in the individual module build.gradle files
    }
}

apply from: 'gradle/spotless.gradle'

allprojects {
    repositories {
        google {
            content {
                includeGroup "com.google"
                includeGroup "com.android"
                includeGroupByRegex "com.google\\..*"
                includeGroupByRegex "com.android\\..*"
                includeGroupByRegex "androidx\\..*"
            }
        }
        mavenCentral()
        jcenter() {
            content {
                includeGroup "org.jetbrains.trove4j"
                includeGroup "com.jraska"
                includeGroup "tools.fastlane"
            }
        }
    }

    afterEvaluate {
        tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).configureEach {
            kotlinOptions {
                jvmTarget = "1.8"
                allWarningsAsErrors = true
            }
        }

        project.plugins.withId("com.android.base") {
            android {
                compileSdkVersion 30
                defaultConfig {
                    minSdkVersion 23
                    targetSdkVersion 30
                }
                compileOptions {
                    sourceCompatibility JavaVersion.VERSION_1_8
                    targetCompatibility JavaVersion.VERSION_1_8
                }
            }
        }
    }
}
