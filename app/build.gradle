apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: "kotlin-kapt"
apply plugin: "androidx.navigation.safeargs.kotlin"
apply plugin: "com.github.triplet.play"
apply plugin: "com.osacky.fladle"
apply plugin: 'com.google.firebase.appdistribution'
apply plugin: 'ru.cian.huawei-publish'
apply from: rootProject.file("jacoco.gradle")

version = "2.5.9"
def buildVersionCode = System.getenv("BUILD_ID") != null ? System.getenv("BUILD_ID").toInteger() : 125210

def getGitHash = { ->
    def stdout = new ByteArrayOutputStream()
    exec {
        commandLine 'git', 'rev-parse', '--short', 'HEAD'
        standardOutput = stdout
    }
    return stdout.toString().trim()
}

android {

    defaultConfig {
        applicationId "nl.rijksoverheid.en"
        versionCode buildVersionCode
        versionName version

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        testOptions.unitTests.includeAndroidResources = true

        resConfigs "nl", "en", "de", "pl", "ro", "tr", "fr", "es", "bg", "ar", "fy"

        buildConfigField "boolean", "FEATURE_LOGGING", "false"
        buildConfigField "boolean", "FEATURE_DEBUG_NOTIFICATION", "false"
        buildConfigField "boolean", "FEATURE_SECURE_SCREEN", "true"
    }

    testOptions {
        unitTests.includeAndroidResources = true
        unitTests.all {
            // https://github.com/robolectric/robolectric/issues/5115
            systemProperty("javax.net.ssl.trustStoreType", "JKS")

            // https://github.com/robolectric/robolectric/issues/5456
            systemProperty("robolectric.dependency.repo.id", "central")
            systemProperty("robolectric.dependency.repo.url", "https://repo1.maven.org/maven2")
        }
    }

    lintOptions {
        // Disable InvalidFragmentVersionForActivityResult for now due to false positives.
        // See https://issuetracker.google.com/issues/182388985#comment9
        // Re-enable after updating to next androidx.activity:activity-ktx version
        disable 'InvalidFragmentVersionForActivityResult'
    }

    packagingOptions {
        exclude 'kotlin/**'
        exclude '**/*.kotlin_metadata'
        exclude 'META-INF/*.kotlin_module'
        exclude 'META-INF/*.version'
        exclude 'DebugProbesKt.bin'
        exclude 'build-data.properties'
        exclude 'play-**.properties'
    }

    signingConfigs {
        release {
            storeFile new File(System.getenv("KEYSTORE_FILE") != null ? System.getenv("KEYSTORE_FILE") : "dummy-keystore.jks")
            storePassword System.getenv("KEYSTORE_PASSWORD")
            keyAlias System.getenv("KEYSTORE_KEY_ALIAS")
            keyPassword System.getenv("KEYSTORE_KEY_PASSWORD")
        }
    }

    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
            buildConfigField "String", "GIT_VERSION", "\"${getGitHash()}\""
        }

        debug {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            buildConfigField "boolean", "FEATURE_LOGGING", "true"
            buildConfigField "String", "GIT_VERSION", "\"dev\""
        }
    }

    flavorDimensions "environment"

    productFlavors {
        dev {
            dimension "environment"
            buildConfigField "boolean", "FEATURE_SECURE_SCREEN", "false"
            buildConfigField "boolean", "FEATURE_DEBUG_NOTIFICATION", "true"
            versionNameSuffix "-dev"
        }
        tst {
            dimension "environment"
            buildConfigField "boolean", "FEATURE_SECURE_SCREEN", "false"
            buildConfigField "boolean", "FEATURE_LOGGING", "true"
            buildConfigField "boolean", "FEATURE_DEBUG_NOTIFICATION", "true"
            versionNameSuffix "-tst"
        }
        pentest {
            dimension "environment"
            buildConfigField "boolean", "FEATURE_SECURE_SCREEN", "false"
            buildConfigField "boolean", "FEATURE_LOGGING", "true"
            buildConfigField "boolean", "FEATURE_DEBUG_NOTIFICATION", "true"
            versionNameSuffix "-pentest"
        }
        acc {
            dimension "environment"
            buildConfigField "boolean", "FEATURE_LOGGING", "true"
            versionNameSuffix "-acc"
        }
        prod {
            dimension "environment"
        }
    }

    compileOptions.coreLibraryDesugaringEnabled true

    buildFeatures {
        viewBinding true
        dataBinding true
    }

    variantFilter { variant ->
        def names = variant.flavors*.name
        if (variant.buildType.name == 'debug' && names.contains("prod")) {
            setIgnore(true)
        }
    }

    firebaseAppDistribution {
        appId = "1:937920499570:android:9ac7b5cf66523282f957eb"
        releaseNotesFile = rootProject.file("app/src/main/play/release-notes/nl-NL/internal.txt")
        groups = "android-latest"
    }

    kotlinOptions {
        freeCompilerArgs += ["-Xopt-in=kotlinx.coroutines.ExperimentalCoroutinesApi"]
    }
}

play {
    enabled = System.getenv("PLAY_STORE_CREDENTIALS") != null
    serviceAccountCredentials = System.getenv("PLAY_STORE_CREDENTIALS") != null ? new File(System.getenv("PLAY_STORE_CREDENTIALS")) : null
    track = "internal"
}

huaweiPublish {
    instances {
        prodRelease {
            buildFormat = "aab"
            publish = false
        }
    }
}

fladle {
    projectId = "vws-coronamelder"
    variant = "devDebug"
    testShards = 1
    devices = [
            ["model": "Pixel2", "version": "28"]
    ]
    testTargets = ["notAnnotation nl.rijksoverheid.en.screenshots.StoreScreenshots"]
}

repositories {
    maven {
        url "https://jitpack.io"
        content {
            includeGroup "com.github.pandulapeter.beagle"
            includeGroup "com.github.blipinsk"
            includeGroup "com.github.lisawray.groupie"
        }
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar', '*.aar'])

    // Java 8+ API desugaring support
    coreLibraryDesugaring 'com.android.tools:desugar_jdk_libs:1.1.5'

    // Service Api
    implementation project(":api")
    // ExposureNotificationApi
    implementation project(":en-api")
    // ResponseSignatureValidator
    implementation project(":signing")

    // AndroidX
    implementation "androidx.appcompat:appcompat:$androidx_appcompat_version"
    implementation "androidx.activity:activity-ktx:$androidx_activity_version"
    implementation "androidx.fragment:fragment-ktx:$androidx_fragment_version"
    implementation "androidx.core:core-ktx:$androidx_core_version"

    // Splashscreen
    implementation 'androidx.core:core-splashscreen:1.0.0-beta02'

    // ConstraintLayout
    implementation 'androidx.constraintlayout:constraintlayout:2.1.3'

    // CustomTabs
    implementation "androidx.browser:browser:1.4.0"

    // Lifecycle components
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:$androidx_lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-livedata-ktx:$androidx_lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-common-java8:$androidx_lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-process:$androidx_lifecycle_version"

    // WorkManager (UI independent background tasks)
    implementation "androidx.work:work-runtime-ktx:$androidx_work_version"

    // SharedPreferences
    implementation "androidx.preference:preference-ktx:1.2.0"
    // EncryptedSharedPreferences
    implementation "androidx.security:security-crypto:1.1.0-alpha03"

    // Navigation components
    implementation "androidx.navigation:navigation-fragment-ktx:$androidx_nav_version"
    implementation "androidx.navigation:navigation-ui-ktx:$androidx_nav_version"
    androidTestImplementation "androidx.navigation:navigation-testing:$androidx_nav_version"


    // Timber logger
    implementation "com.jakewharton.timber:timber:$timber_version"

    // Material components
    implementation 'com.google.android.material:material:1.5.0'

    // Google Play Core Library
    implementation "com.google.android.play:core:1.10.3"
    implementation "com.google.android.play:core-ktx:1.8.1"

    // Google play services
    implementation "com.google.android.gms:play-services-base:$play_services_base_version"
    implementation "com.google.android.gms:play-services-basement:$play_services_base_version"
    implementation "com.google.android.gms:play-services-tasks:$play_services_tasks_version"

    // Lottie animations
    implementation 'com.airbnb.android:lottie:4.2.2'

    // Groupie (RecyclerView builder)
    implementation "com.github.lisawray.groupie:groupie:$groupie_version"
    implementation "com.github.lisawray.groupie:groupie-viewbinding:$groupie_version"

    // Insetter (WindowInsets util)
    implementation "dev.chrisbanes.insetter:insetter:$insetter_version"

    // UnitTest support classes
    testImplementation project(":test-support")
    debugImplementation project(":test-support")
    androidTestDebugImplementation project(":test-support")

    // Unit testing
    testImplementation "junit:junit:$junit_version"
    testImplementation "androidx.arch.core:core-testing:$arch_core_version"
    testImplementation "androidx.work:work-testing:$androidx_work_version"

    // Mockito
    testImplementation "org.mockito.kotlin:mockito-kotlin:$mockitokotlin_version"
    testImplementation "org.mockito:mockito-core:$mockito_version"
    androidTestImplementation "org.mockito:mockito-android:$mockito_version"
    androidTestImplementation "org.mockito.kotlin:mockito-kotlin:$mockitokotlin_version"

    // MockWebServer
    testImplementation "com.squareup.okhttp3:mockwebserver:$okhttp3_version"

    // Coroutine testing util
    testImplementation "org.jetbrains.kotlinx:kotlinx-coroutines-test:$coroutines_version"

    // Roboelectric test runner
    testImplementation "org.robolectric:robolectric:$robolectric_version"

    // AndroidTests
    androidTestImplementation "androidx.arch.core:core-testing:$arch_core_version"
    androidTestImplementation "androidx.test.ext:junit:$androidx_test_ext_junit_version"

    // Espresso (Android UI tests)
    androidTestImplementation "androidx.test.espresso:espresso-core:$androidx_espresso_version"
    androidTestImplementation "androidx.test.espresso:espresso-contrib:$androidx_espresso_version"
    androidTestImplementation "androidx.test.espresso:espresso-intents:$androidx_espresso_version"

    // UI Automator
    androidTestImplementation 'androidx.test.uiautomator:uiautomator:2.2.0'

    // JUnit TestRule for Android instrumented tests, which automatically disables and enables animations
    androidTestImplementation 'com.github.blipinsk:disable-animations-rule:2.0.0'
    // JUnit rule for specifying some of the UI demo commands and customize some part of the Status as well as the Navigation bar
    androidTestImplementation 'com.vanniktech:junit4-android-integration-rules:0.3.0'

    // Fastlane Screengrab: localized screenshots
    androidTestImplementation 'tools.fastlane:screengrab:2.1.1'

    // Beagle debug drawer
    def beagleVersion = "2.5.1"
    tstImplementation "com.github.pandulapeter.beagle:ui-drawer:$beagleVersion"
}
