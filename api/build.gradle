apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply from: rootProject.file("jacoco.gradle")

ext.cdnEndpoint = ext.has("cdnEndpoint") ? ext.cdnEndpoint : "http://localhost/"
ext.apiEndpoint = ext.has("apiEndpoint") ? ext.apiEndpoint : "http://localhost/"

android {

    defaultConfig {
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles "consumer-rules.pro"

        // encoded query string for public read access on the cdn
        buildConfigField "String", "CDN_BASE_URL", "\"https://test.coronamelder-dist.nl/\""
        buildConfigField "String", "API_BASE_URL", "\"https://test.coronamelder-api.nl/\""
        buildConfigField "boolean", "FEATURE_RESPONSE_SIGNATURES", "true"
        buildConfigField "boolean", "FEATURE_SSL_PINNING", "true"
    }

    testOptions {
        unitTests.includeAndroidResources = true
        unitTests.all {
            // https://github.com/robolectric/robolectric/issues/5115
            systemProperty("javax.net.ssl.trustStoreType", "JKS")

            // https://github.com/robolectric/robolectric/issues/5456
            systemProperty("robolectric.dependency.repo.id", "central")
            systemProperty("robolectric.dependency.repo.url", "https://repo1.maven.org/maven2")
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }

        debug {
            // Uncomment to disable pinning for all debug variants
            // buildConfigField "boolean", "FEATURE_SSL_PINNING", "false"
        }
    }

    flavorDimensions "environment"

    productFlavors {
        dev {
            dimension "environment"
            buildConfigField "boolean", "FEATURE_RESPONSE_SIGNATURES", "false"
            buildConfigField "boolean", "FEATURE_SSL_PINNING", "false"
            buildConfigField "String", "CDN_BASE_URL", "\"${cdnEndpoint}\""
            buildConfigField "String", "API_BASE_URL", "\"${apiEndpoint}\""
        }
        tst {
            dimension "environment"
        }
        pentest {
            dimension "environment"
            buildConfigField "boolean", "FEATURE_SSL_PINNING", "false"
        }
        acc {
            dimension "environment"
            buildConfigField "String", "CDN_BASE_URL", "\"https://acceptatie.coronamelder-dist.nl/\""
            buildConfigField "String", "API_BASE_URL", "\"https://acceptatie.coronamelder-api.nl/\""
        }
        prod {
            dimension "environment"
            buildConfigField "String", "CDN_BASE_URL", "\"https://productie.coronamelder-dist.nl/\""
            buildConfigField "String", "API_BASE_URL", "\"https://coronamelder-api.nl/\""
        }
    }
}

dependencies {
    implementation fileTree(dir: "libs", include: ["*.jar"])

    // ResponseSignatureValidator
    implementation project(":signing")

    // Coroutines
    api "org.jetbrains.kotlinx:kotlinx-coroutines-android:$coroutines_version"

    // Retrofit
    api "com.squareup.retrofit2:retrofit:$retrofit_version"
    implementation "com.squareup.retrofit2:converter-moshi:$retrofit_version"

    // OkHttp
    api "com.squareup.okhttp3:okhttp:$okhttp3_version"
    implementation "com.squareup.okhttp3:logging-interceptor:$okhttp3_version"

    // Moshi Json parsing
    implementation("com.squareup.moshi:moshi-kotlin:$moshi_version") {
        exclude group: "org.jetbrains.kotlin", module: "kotlin-reflect"
    }
    implementation "com.squareup.moshi:moshi-adapters:$moshi_version"
    kapt "com.squareup.moshi:moshi-kotlin-codegen:$moshi_version"

    // Timber logger
    implementation "com.jakewharton.timber:timber:$timber_version"

    // JUnit
    testImplementation "junit:junit:$junit_version"
    testImplementation "androidx.test:core:$androidx_test_core_version"

    // OkHttp mock webserver
    testImplementation "com.squareup.okhttp3:mockwebserver:$okhttp3_version"
    // OkHttp-TLS: Used by TLS related unit tests
    testImplementation "com.squareup.okhttp3:okhttp-tls:$okhttp3_version"
    // Roboelectr test runner
    testImplementation "org.robolectric:robolectric:$robolectric_version"
}
